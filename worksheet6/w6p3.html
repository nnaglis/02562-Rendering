<!DOCTYPE html>
<html>

<head>
<title > W6P3</title>
<script type="text/javascript" src="../common/MV.js"></script>
<script type="text/javascript" src="../common/OBJParser_interleaved.js"></script>
<script type="text/javascript" src="../common/Aabb.js"></script>
<script type="text/javascript" src="../common/BspTree_interleaved.js"></script>
<script type = "text/javascript" src = "w6p3.js"></script>
</head>

<body>
<div>
    <label for="options">Plane/triangle shader:</label>
        <select id="planeTriangleOptions" name="planeTriangleOptions">
            <option value="Lambertian" selected>Lambertian</option>
            <option value="Phong">Phong</option>
            <option value="Mirror">Mirror</option>
            <option value="Base">Base</option>
        </select>
    <div>
        <select id="addressmode" size="2">
                <option value="0">clamp-to-edge</option>
                <option value="1" selected>repeat</option>
            </select>
        <select id="filtermode" size="2">
                <option value="0">nearest</option>
                <option value="1" selected>linear</option>
            </select>

        <label for="useTexture">Use Texture:</label>
        <input type="checkbox" id="useTexture" name="useTexture" value="1" checked>
        

        <label for="pixelSubdivision">Pixel Subdivision:</label>
        <input type="range" id="pixelSubdivision" name="pixelSubdivision" min="1" max="10" value="1" oninput="this.nextElementSibling.value = this.value">
        <output>1</output>
    </div>
    
    <br>
<canvas id = "webgpu-canvas" width = "512" height = "512">
<script id = "wgsl" type = "x-shader">

const PI = 3.14;

const PLANE_POSITION: vec3f = vec3f(0.0, 0.0, 0.0);
const PLANE_COLOR : vec3f = vec3f(0.1, 0.7, 0.0);
const SPHERE_COLOR: vec3f = vec3f(0.2, 0.2, 0.2);
const TRIANGLE_COLOR: vec3f = vec3f(0.9, 0.9, 0.9);
const TRIANGLE_VERTICES: array<vec3f, 3> = array<vec3f, 3>(
    vec3f(-0.2, 0.1, 0.9),  // First vector
    vec3f(0.2, 0.1, 0.9),  // Second vector
    vec3f(-0.2, 0.1, -0.1)   // Third vector
);

const LIGHT_POSITION: vec3f = vec3f(0.0, 1.0, 0.0);
const DIRECTIONAL_LIGHT_DIRECTION: vec3f = vec3f(-1.0);
//const LIGHT_INTENSITY: vec3f = vec3f(PI, PI, PI);
const LIGHT_INTENSITY: vec3f = vec3f(50, 50, 50);

const DIFFUSE_REFLECTANCE: vec3f = vec3f(0.9);
const AMBIENT_LIGHT: vec3f = vec3f(0.1);

const T_MIN = 10e-2;
const T_MAX = 1.0e32;

const IOR_GLASS = 1.5;
const IOR_AIR = 1.0;

const SPECULAR_SPHERE: f32 = 0.1;
const SHININESS_SPHERE: f32 = 6.0;

const TEXTURE_SCALE: f32 = 0.2;



struct Uniforms {
    eyePos: vec3<f32>,
    const_cam: f32, // Placed next to eyePos to avoid padding
    lookAt: vec3<f32>,
    aspect: f32, // Placed next to lookAt to avoid padding
    up: vec3<f32>,
    padding: f32,
};

struct Uniforms_uint {
    sphere_shader: u32,
    plane_triangle_shader: u32,
    use_repeat: u32,
    use_linear: u32,
    use_texture: u32,
    pixel_subdivision: u32,
};

struct Material {
    color: vec3<f32>,
    emission: vec3<f32>,
    specular: vec3<f32>,
    illum: f32,
    padding3: vec3<f32>,
};



@group(0) @binding(0) var<uniform> uniforms : Uniforms;
@group(0) @binding(1) var<uniform> uniforms_uint : Uniforms_uint;
//@group(0) @binding(2) var my_texture: texture_2d<f32>;
@group(0) @binding(3) var<storage> jitter: array<vec2f>;
@group(0) @binding(4) var<storage> vertexAttributes : array<Vertex_attr>;
@group(0) @binding(5) var<storage> meshFaces : array<vec4u>;
//@group(0) @binding(6) var<storage> vNormals : array<vec3f>;
@group(0) @binding(7) var<storage> materials : array<Material>;
//@group(0) @binding(8) var<storage> material_indices : array<u32>;
@group(0) @binding(9) var<storage> lightIndices : array<u32>;
@group(0) @binding(10) var<uniform> aabb: Aabb;
@group(0) @binding(11) var<storage> treeIds: array<u32>;
@group(0) @binding(12) var<storage> bspTree: array<vec4u>;
@group(0) @binding(13) var<storage> bspPlanes: array<f32>;

const MAX_LEVEL = 20u;
const BSP_LEAF = 3u;
var<private> branch_node: array<vec2u, MAX_LEVEL>;
var<private> branch_ray: array<vec2f, MAX_LEVEL>;

struct Vertex_attr {
    vPosition: vec3<f32>,
    vNormal: vec3<f32>,
    };

struct Aabb {
    min: vec3f, max: vec3f, 
};

struct VSOut {
    @builtin(position) position : vec4f,
    @location(0) coords : vec2f,
};

struct Ray {
    origin: vec3f,
    direction: vec3f,
    tmin: f32,
    tmax: f32,
};

struct HitInfo {
    has_hit: bool,
    dist: f32,
    position: vec3f,
    normal: vec3f,
    color: vec3f,
    shader: u32,
    ior1_over_ior2: f32,
    specular: f32,
    shininess: f32,
    ambient: vec3f,
    diffuse: vec3f,
    texcoords: vec2f,
};

struct Light {
    L_i: vec3f,
    w_i: vec3f,
    dist: f32
};

fn get_camera_ray(ipcoords: vec2f) -> Ray
{
    let v = normalize(uniforms.lookAt - uniforms.eyePos);
    let u = uniforms.up;
    var b1 = normalize(cross(v, u));
    let b2 = cross(b1, v);
    return Ray(uniforms.eyePos, normalize(b1 * ipcoords.x + b2 * ipcoords.y + v * uniforms.const_cam), T_MIN, T_MAX);
}


fn intersect_sphere(ray: ptr<function, Ray>, hit: ptr<function, HitInfo>, center: vec3f, radius: f32, shader: u32) -> bool
{
    let b_2 = dot(ray.origin - center, ray.direction);
    let c = dot((ray.origin - center), (ray.origin - center)) - (radius*radius);
    if ( (b_2*b_2 - c) < 0.0)
    {
        return false; // no intersection
    }
    let t_1 = -b_2 - sqrt((b_2*b_2)-c);
    let t_2 = -b_2 + sqrt((b_2*b_2)-c);

    var t = ray.tmax; // Some large number to represent infinity
    if t_1 > 0.0 {
        if t_1 >= ray.tmin && t_1 <= ray.tmax {
            t = t_1; // Use t_1 if it's valid
        }
    }
    if t_2 > 0.0 && t_2 < t {
            if t_2 >= ray.tmin && t_2 <= ray.tmax {
            t = t_2; // Use t_1 if it's valid
        }   
    }

    if t <= ray.tmin || t >= ray.tmax {
        return false; // t is not within ray bounds
    }
    //update hit info
    (*ray).tmax = t;
    (*hit).has_hit = true;
    (*hit).dist = t;
    (*hit).position = ray.origin + t * ray.direction;
    (*hit).normal = normalize((*hit).position - center);
    (*hit).color = SPHERE_COLOR;
    (*hit).shader = shader;
    (*hit).ambient = AMBIENT_LIGHT;
    (*hit).diffuse = DIFFUSE_REFLECTANCE;
    (*hit).specular = SPECULAR_SPHERE;
    (*hit).shininess = SHININESS_SPHERE;
    return true;
    
}

fn intersect_triangle(ray: ptr<function, Ray>, hit: ptr<function, HitInfo>, faceIndex: u32,) -> bool 
{
    let face = meshFaces[faceIndex];
    let e0 = vertexAttributes[face[1]].vPosition - vertexAttributes[face[0]].vPosition;
    let e1 = vertexAttributes[face[2]].vPosition - vertexAttributes[face[0]].vPosition;

    var normal = cross(e0, e1);
    let beta = dot(cross((vertexAttributes[face[0]].vPosition - ray.origin), ray.direction), e1) / (dot(ray.direction, normal));

    let gamma = - dot( cross(vertexAttributes[face[0]].vPosition - ray.origin, ray.direction), e0) / dot(ray.direction,normal);

    if (beta < 0.0) || (gamma < 0.0) || (beta+gamma>1.0)
    {
        return false;
    }
    let weighted_normal = normalize( vertexAttributes[face[0]].vNormal * (1.0 - beta - gamma) + vertexAttributes[face[1]].vNormal * beta + vertexAttributes[face[2]].vNormal * gamma);   
    let t = dot((vertexAttributes[face[0]].vPosition - ray.origin), normal) / dot(ray.direction,normal);
    if  (t <= ray.tmin || t >= ray.tmax)
    {
        return false;
    }


    //update hit info
    (*ray).tmax = t;
    (*hit).has_hit = true;
    (*hit).dist = t;
    (*hit).position = ray.origin + t * ray.direction;
    (*hit).normal = weighted_normal;
    (*hit).color = materials[face[3]].color; 
    (*hit).shader = uniforms_uint.plane_triangle_shader;
    (*hit).ambient = materials[face[3]].emission;
    (*hit).diffuse = DIFFUSE_REFLECTANCE;
    (*hit).specular = SPECULAR_SPHERE;
    (*hit).shininess = SHININESS_SPHERE;
    return true;

}

fn intersect_min_max(r: ptr<function, Ray>) -> bool
{
    let p1 = (aabb.min - r.origin)/r.direction;
    let p2 = (aabb.max - r.origin)/r.direction;
    let pmin = min(p1, p2);
    let pmax = max(p1, p2);
    let tmin = max(pmin.x, max(pmin.y, pmin.z));
    let tmax = min(pmax.x, min(pmax.y, pmax.z));
    if(tmin > tmax || tmin > r.tmax || tmax < r.tmin) {
    return false;
        }
    r.tmin = max(tmin - 1.0e-3f, r.tmin);
    r.tmax = min(tmax + 1.0e-3f, r.tmax);
    return true;
}

fn intersect_trimesh(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> bool {
    var branch_lvl = 0u;
    var near_node = 0u;
    var far_node = 0u;
    var t = 0.0f;
    var node = 0u;
    for(var i = 0u; i <= MAX_LEVEL; i++) {
        let tree_node = bspTree[node];
        let node_axis_leaf = tree_node.x&3u;
        if(node_axis_leaf == BSP_LEAF) {
            // A leaf was found
            let node_count = tree_node.x>>2u;
            let node_id = tree_node.y;
            var found = false;
            for(var j = 0u; j < node_count; j++) {
                let obj_idx = treeIds[node_id + j];
                if(intersect_triangle(r, hit, obj_idx)) {
                    r.tmax = hit.dist;
                    found = true;
                    }
                }
                if(found) {
                    
                    return true; 
                    }
                else if(branch_lvl == 0u) { return false; }
                else {
                    branch_lvl--;
                    i = branch_node[branch_lvl].x;
                    node = branch_node[branch_lvl].y;
                    r.tmin = branch_ray[branch_lvl].x;
                    r.tmax = branch_ray[branch_lvl].y;
                    continue;
                }
              }
        let axis_direction = r.direction[node_axis_leaf];
        let axis_origin = r.origin[node_axis_leaf];
        if(axis_direction >= 0.0f) {
            near_node = tree_node.z; // left
            far_node = tree_node.w; // right
        }
        else {
            near_node = tree_node.w; // right
            far_node = tree_node.z; // left
        }
        let node_plane = bspPlanes[node];
        let denom = select(axis_direction, 1.0e-8f, abs(axis_direction) < 1.0e-8f);
        t = (node_plane - axis_origin)/denom;
        if(t > r.tmax) { node = near_node; }
        else if(t < r.tmin) { node = far_node; }
        else {
            branch_node[branch_lvl].x = i;
            branch_node[branch_lvl].y = far_node;
            branch_ray[branch_lvl].x = t;
            branch_ray[branch_lvl].y = r.tmax;
            branch_lvl++;
            r.tmax = t;
            node = near_node;
        }
    }
        return false;
}

fn intersect_scene(ray: ptr<function, Ray>, hit: ptr<function, HitInfo>, use_repeat: bool, use_linear: bool, use_texture: bool) -> bool 
{

// Define scene data as constants.
// Call an intersection function for each object.
// For each intersection found, update (*r).tmax and store additional info about the hit.
    //intersect_plane(ray, hit, PLANE_POSITION, plane_onb, use_repeat, use_linear, use_texture);
    //
    
    //intersect_min_max(ray);
    //intersect_trimesh(ray, hit);
    if (intersect_min_max(ray)) {
    intersect_sphere(ray, hit, vec3f(420.0, 90.0, 370.0), 90.0, 3);
    intersect_sphere(ray, hit, vec3f(130.0, 90.0, 250.0), 90.0, 4);
        intersect_trimesh(ray, hit);
        
        
    }

    return (*hit).has_hit;
}

fn sample_point_light(pos: vec3f) -> Light
{
    let L_i = LIGHT_INTENSITY / ( length(LIGHT_POSITION - pos) * length(LIGHT_POSITION - pos));
    let w_i = normalize(LIGHT_POSITION - pos);
    let dist = length(LIGHT_POSITION - pos);
    return Light(L_i, w_i, dist);
}

fn sample_directional_light() -> Light
{
    return Light(LIGHT_INTENSITY, normalize(-DIRECTIONAL_LIGHT_DIRECTION), T_MAX);
}

fn sample_area_light(triangeIndex: u32, hit: ptr<function, HitInfo>) -> Light
{

    let face = meshFaces[triangeIndex];
    let centroid = (vertexAttributes[face[0]].vPosition + vertexAttributes[face[1]].vPosition + vertexAttributes[face[2]].vPosition) / 3.0;
    let w_i = normalize(centroid - (*hit).position);
    //calculate the interpolated normal of the light triangle

    let edge1 = vertexAttributes[face[1]].vPosition - vertexAttributes[face[0]].vPosition;
    let edge2 = vertexAttributes[face[2]].vPosition - vertexAttributes[face[0]].vPosition;
    let area = 0.5 * length(cross(edge1, edge2));
    let lightNormal = normalize(cross(edge1, edge2));
    let cosThetaE = max(-dot(w_i, lightNormal), 0.0);

    let dist = length(centroid - (*hit).position);
    let L_e = materials[face[3]].emission;
    let L_i = area * cosThetaE * L_e / (dist * dist);
    return Light(L_i, w_i, dist);
}
fn shade(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    switch (*hit).shader {
        case 1 { return lambertian(r, hit); }
        case 2 { return phong(r, hit); }
        case 3 { return mirror(r, hit); }
        case 4 { return refraction(r, hit); }
        case 5 { return glossy(r, hit); }
        case default { return (*hit).color + (*hit).ambient; }
    }
}

fn lambertian(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f {
    // Initialize ambient contribution
    var L_o = (*hit).ambient;
    var L_rd = vec3f(0.0);

    // Initialize aggregated light properties
    var avg_light_dir = vec3f(0.0);
    var avg_light_intensity = vec3f(0.0);
    var min_distance = T_MAX;
    var lightContributions = 0;

    // Aggregate light sources
    for (var i: u32 = 0; i < arrayLength(&lightIndices); i = i + 1) {
        let light = sample_area_light(lightIndices[i], hit);

        // Accumulate light parameters
        avg_light_dir += light.w_i; // Add light direction
        avg_light_intensity += light.L_i; // Add light intensity
        min_distance = min(min_distance, light.dist); // Find minimum distance
        lightContributions += 1;
    }

    // If there are no lights, return ambient contribution
    if (lightContributions == 0) {
        return L_o;
    }

    // Average the light properties
    avg_light_dir = normalize(avg_light_dir / f32(lightContributions)); // Normalize after averaging
    avg_light_intensity = avg_light_intensity / f32(lightContributions);

    // Treat the averaged light as a single light source
    // light is for certain not averaged in the most optimal way, requiring a large offset to avoid self-shadowing, but nonetheless logic wise is correctly set up
    var shadow_ray = Ray((*hit).position, avg_light_dir, T_MIN, min_distance - (T_MIN * 20.0));
    var shadow_hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 0, 0, 0, vec3f(0.0), vec3f(0.0), vec2f(0.0));
    
    // Check for shadow occlusion
    if (!intersect_scene(&shadow_ray, &shadow_hit, false, false, false)) {
        let n = normalize((*hit).normal);
        L_rd += (*hit).diffuse / PI * avg_light_intensity * max(dot(n, avg_light_dir), 0.0) * (*hit).color * f32(lightContributions);
    }

    return L_rd + L_o;
}


//not used as of P1
fn phong(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    //let light = sample_point_light((*hit).position);
    let light = sample_directional_light();
    var shadow_ray = Ray((*hit).position, light.w_i, T_MIN, light.dist);
    var shadow_hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0),0,0,0,0,vec3f(0.0),vec3f(0.0),vec2f(0.0));
    if (intersect_scene(&shadow_ray, &shadow_hit, false, false, false))
    {
        if ((shadow_hit.dist) < (*hit).dist)
        {
            return vec3f(0.0);
        }
    }
    let k_d = (*hit).diffuse / PI * (*hit).color;
    let k_s = (*hit).specular * ((*hit).shininess + 2.0) / (2.0 * PI);
    let n = normalize((*hit).normal);
    let w_r = reflect(-light.w_i, n);
    let w_o = -r.direction;
    let L_r = light.L_i * (k_d+ k_s * pow(max(dot(w_r, w_o), 0.0), (*hit).shininess)) * dot(n, light.w_i);
    return L_r;
   // return test_L;
}

fn mirror(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    //let light = sample_point_light((*hit).position);
    let direction = reflect(r.direction, (*hit).normal);
    *r = Ray((*hit).position, direction, T_MIN, T_MAX);
    (*hit).has_hit = false;
    (*hit).color = vec3f(0.0, 0.0, 0.0);
    return (*hit).color;

}

fn refraction(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    // Find if the ray hits from inside or outside
    let n1 = IOR_AIR; 
    let n2 = IOR_GLASS;
    var cos_theta_i = dot(-r.direction, (*hit).normal);
    
    //if the ray from inside of the object, swap the normal and the ior
    if cos_theta_i < 0.0
    {
        cos_theta_i = -cos_theta_i;
        (*hit).normal = -(*hit).normal;
        (*hit).ior1_over_ior2 = n2/n1;
    }
    else
    {
        (*hit).ior1_over_ior2 = n1/n2;
    }

    let cos_theta_t2 = 1.0 - ((*hit).ior1_over_ior2 * (*hit).ior1_over_ior2 * (1.0 - cos_theta_i * cos_theta_i));
    
    // Check for total internal reflection
    if (cos_theta_t2 < 0.0)
    {
        return mirror(r, hit);
    }

    let refracted_direction = 
        (*hit).ior1_over_ior2 * (cos_theta_i * (*hit).normal - (-r.direction)) - 
        (*hit).normal * sqrt(cos_theta_t2);

    // all of the above is equivalent to the following built-in webgpu function ....
    //let refracted_direction_t = refract(r.direction, (*hit).normal, (*hit).ior1_over_ior2);

    *r = Ray((*hit).position, normalize(refracted_direction), T_MIN, T_MAX);
    
    (*hit).has_hit = false;
    return vec3f(0.0, 0.0, 0.0);
}

fn glossy(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    // call phong and refraction functions and mix the results
    let phong_result = phong(r, hit);
    let refraction_result = refraction(r, hit);
    return phong_result + refraction_result;
}

fn texture_nearest(texture: texture_2d<f32>, texcoords: vec2f, repeat: bool) -> vec3f {
    let res = textureDimensions(texture);
    let st = select(clamp(texcoords, vec2f(0), vec2f(1)), texcoords - floor(texcoords), repeat);
    let ab = st*vec2f(res);
    let UV = vec2u(ab + 0.5) % res;
    let texcolor = textureLoad(texture, UV, 0);
    return texcolor.rgb;
}

fn texture_linear(texture: texture_2d<f32>, texcoords: vec2f, repeat: bool) -> vec3f {
    let res = textureDimensions(texture);
    let st = select(clamp(texcoords, vec2f(0), vec2f(1)), texcoords - floor(texcoords), repeat);
    let ab = st*vec2f(res);
    // find the four nearest texels
    let UV = vec2u(ab);
    let texcolor = textureLoad(texture, UV, 0);
    let texcolor1 = textureLoad(texture, vec2u(UV.x+1, UV.y), 0);
    let texcolor2 = textureLoad(texture, vec2u(UV.x, UV.y+1), 0);
    let texcolor3 = textureLoad(texture, vec2u(UV.x+1, UV.y+1), 0);
    // interpolate between the four texels
    let frac = ab - vec2f(UV);
    let texcolor4 = mix(texcolor, texcolor1, frac.x);
    let texcolor5 = mix(texcolor2, texcolor3, frac.x);
    let texcolor6 = mix(texcolor4, texcolor5, frac.y);
    return texcolor6.rgb;
}

fn texture(texture: texture_2d<f32>, texcoords: vec2f, repeat: bool, linear: bool) -> vec3f {
    if (linear)
    {
        return texture_linear(texture, texcoords, repeat);
    }
    else
    {
        return texture_nearest(texture, texcoords, repeat);
    }
}



@vertex
fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut 
{
    const pos = array<vec2f, 4 > (vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
    var vsOut : VSOut;
    vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
    vsOut.coords = pos[VertexIndex];
    return vsOut; 
}




@fragment
fn main_fs(@location(0) coords: vec2f) -> @location(0) vec4f
{
    let use_repeat = uniforms_uint.use_repeat != 0;
    let use_linear = uniforms_uint.use_linear != 0;
    let use_texture = uniforms_uint.use_texture != 0;
    const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
    const max_depth = 10;
    let base_uv = vec2f(coords.x*uniforms.aspect*0.5f, coords.y*0.5f);
    // Add jitter to the pixel calculation
    var pixel_subdivision = uniforms_uint.pixel_subdivision;
    var result = vec3f(0.0);
    for (var i: u32 = 0; i < pixel_subdivision; i++)
    {
        for (var j: u32 = 0; j < pixel_subdivision; j++)
        {
            let idx = (i*pixel_subdivision + j) * 2;
            let uv = base_uv + jitter[idx];
            var r = get_camera_ray(uv);
            var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0),0,0,0,0,vec3f(0.0),vec3f(0.0),vec2f(0.0));
            for(var i = 0; i < max_depth; i++) {
                if(intersect_scene(&r, &hit, use_repeat, use_linear, use_texture))
                { 
                    result += shade(&r, &hit); 
                }
                else 
                { 
                    result += bgcolor.rgb;
                    break; 
                }
                if(hit.has_hit) 
                { 
                    break; 
                }
            }
        }
    }
    
    result = result / (f32(pixel_subdivision) * f32(pixel_subdivision));
    return vec4f(result, 1.0);
    //return vec4f(0.5,0.5,0.5,1.0);
}
    </script>
    Please use a browser that supports HTML5 canvas.
    </canvas>
    </div>
    </body>

    </html>
