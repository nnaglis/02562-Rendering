<!DOCTYPE html>
<html>

<head>
<title > W2P1</title>
<script type="text/javascript" src="../common/MV.js"></script>
<script type = "text/javascript" src = "w2p1.js"></script>
</head>

<body>
<canvas id = "webgpu-canvas" width = "512" height = "512">
<script id = "wgsl" type = "x-shader">

const PI = 3.14;

const PLANE_POSITION: vec3f = vec3f(0.0, 0.0, 0.0);
const PLANE_NORMAL : vec3f = vec3f(0.0, 1.0, 0.0);
const PLANE_COLOR : vec3f = vec3f(0.1, 0.7, 0.0);
const SPHERE_CENTER: vec3f = vec3f(0.0, 0.5, 0.0);
const SPHERE_RADIUS: f32 = 0.3;
const SPHERE_COLOR: vec3f = vec3f(0.0, 0.0, 0.0);
const TRIANGLE_COLOR: vec3f = vec3f(0.4, 0.3, 0.2);
const TRIANGLE_VERTICES: array<vec3f, 3> = array<vec3f, 3>(
    vec3f(-0.2, 0.1, 0.9),  // First vector
    vec3f(0.2, 0.1, 0.9),  // Second vector
    vec3f(-0.2, 0.1, -0.1)   // Third vector
);

const LIGHT_POSITION: vec3f = vec3f(0.0, 1.0, 0.0);
const LIGHT_INTENSITY: vec3f = vec3f(PI, PI, PI);

const DIFFUSE_REFLECTANCE: f32 = 0.9;
const AMBIENT_LIGHT: f32 = 0.1;

const T_MIN = 1.0e-6;
const T_MAX = 1.0e32;


struct Uniforms {
    eyePos: vec3<f32>,
    const_cam: f32, // Placed next to eyePos to avoid padding
    lookAt: vec3<f32>,
    aspect: f32, // Placed next to lookAt to avoid padding
    up: vec3<f32>,
    padding: f32 // Single padding to align the structure
};

@group(0) @binding(0) var<uniform> uniforms : Uniforms;

struct VSOut {
    @builtin(position) position : vec4f,
    @location(0) coords : vec2f,
};

struct Ray {
    origin: vec3f,
    direction: vec3f,
    tmin: f32,
    tmax: f32,
};

struct HitInfo {
    has_hit: bool,
    dist: f32,
    position: vec3f,
    normal: vec3f,
    color: vec3f,
    shader: u32,
};

struct Light {
    L_i: vec3f,
    w_i: vec3f,
    dist: f32
};

fn get_camera_ray(ipcoords: vec2f) -> Ray
{
    let v = normalize(uniforms.lookAt - uniforms.eyePos);
    let u = uniforms.up;
    var b1 = normalize(cross(v, u));
    let b2 = cross(b1, v);
    return Ray(uniforms.eyePos, normalize(b1 * ipcoords.x + b2 * ipcoords.y + v * uniforms.const_cam), T_MIN, T_MAX);
}

fn intersect_plane(ray: ptr<function, Ray>, hit: ptr<function, HitInfo>, position: vec3f, normal: vec3f) -> bool
{
    let t = dot( position - ray.origin, normal) / dot(ray.direction,normal);
    if (t > ray.tmin && t < ray.tmax)
    {
    // update hit info
    (*ray).tmax = t;
    (*hit).has_hit = true;
    (*hit).dist = t;
    (*hit).position = ray.origin + t * ray.direction;
    (*hit).normal = normal;
    (*hit).color = PLANE_COLOR;
    (*hit).shader = 1;
    return true;
    }
    return false;
}

fn intersect_sphere(ray: ptr<function, Ray>, hit: ptr<function, HitInfo>, center: vec3f, radius: f32) -> bool
{
    let b_2 = dot(ray.origin - center, ray.direction);
    let c = dot((ray.origin - center), (ray.origin - center)) - (radius*radius);
    if ( (b_2*b_2 - c) < 0.0)
    {
        return false; // no intersection
    }
    let t_1 = -b_2 - sqrt((b_2*b_2)-c);
    let t_2 = -b_2 + sqrt((b_2*b_2)-c);

    var t = 1e32; // Some large number to represent infinity
    if t_1 > 0.0 {
        t = t_1; // Use t_1 if it's valid
    }
    if t_2 > 0.0 && t_2 < t {
        t = t_2; // Use t_2 if it's valid and smaller than current t
    }

    if t <= ray.tmin || t >= ray.tmax {
        return false; // t is not within ray bounds
    }
    //update hit info
    (*ray).tmax = t;
    (*hit).has_hit = true;
    (*hit).dist = t;
    (*hit).position = ray.origin + t * ray.direction;
    (*hit).normal = normalize((*hit).position - center);
    (*hit).color = SPHERE_COLOR;
    (*hit).shader = 1;
    return true;
    
}

fn intersect_triangle(ray: ptr<function, Ray>, hit: ptr<function, HitInfo>, v: array<vec3f, 3>) -> bool
{
    let e0 = v[1] - v[0];
    let e1 = v[2] - v[0];

    var normal = cross(e0, e1);
    let beta = dot(cross((v[0] - ray.origin), ray.direction), e1) / (dot(ray.direction, normal));

    let gamma = - dot( cross(v[0] - ray.origin, ray.direction), e0) / dot(ray.direction,normal);

    if (beta < 0.0) || (gamma < 0.0) || (beta+gamma>1.0)
    {
        return false;
    }
    let t = dot((v[0] - ray.origin), normal) / dot(ray.direction,normal);
    if  (t <= ray.tmin || t >= ray.tmax)
    {
        return false;
    }
    //update hit info
    (*ray).tmax = t;
    (*hit).has_hit = true;
    (*hit).dist = t;
    (*hit).position = ray.origin + t * ray.direction;
    (*hit).normal = normal;
    (*hit).color = TRIANGLE_COLOR;
    (*hit).shader = 1;
    return true;

}

fn intersect_scene(ray: ptr<function, Ray>, hit : ptr<function, HitInfo>) -> bool
{
// Define scene data as constants.
// Call an intersection function for each object.
// For each intersection found, update (*r).tmax and store additional info about the hit.
    intersect_plane(ray, hit, PLANE_POSITION, PLANE_NORMAL);
    intersect_sphere(ray, hit, SPHERE_CENTER, SPHERE_RADIUS);
    intersect_triangle(ray, hit, TRIANGLE_VERTICES);

    return (*hit).has_hit;
}

fn sample_point_light(pos: vec3f) -> Light
{
    let L_i = LIGHT_INTENSITY / ( length(LIGHT_POSITION - pos) * length(LIGHT_POSITION - pos));
    let w_i = normalize(LIGHT_POSITION - pos);
    let dist = length(LIGHT_POSITION - pos);
    return Light(L_i, w_i, dist);
}



fn shade(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    switch (*hit).shader {
        case 1 { return lambertian(r, hit); }
        case 2 { return phong(r, hit); }
        case 3 { return mirror(r, hit); }
        case default { return (*hit).color; }
    }
}

fn lambertian(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    let light = sample_point_light((*hit).position);
    var shadow_ray = Ray((*hit).position, light.w_i, T_MIN, light.dist);
    var shadow_hit = HitInfo(false, 0.0, vec3f(0.0, 0.0, 0.0), vec3f(0.0, 0.0, 0.0), vec3f(0.0, 0.0, 0.0),0);
    var L_o = AMBIENT_LIGHT * (*hit).color;
    if (intersect_scene(&shadow_ray, &shadow_hit))
    {
        return L_o;
    }
    let n = normalize((*hit).normal);
    let L_rd = DIFFUSE_REFLECTANCE / PI * light.L_i * max(dot(n, light.w_i), 0.0) * (*hit).color;
    L_o += L_rd;
    return L_o;
}

//not used as of P1
fn phong(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{

    return vec3f(1.0, 0.0, 0.0);
}

// not used as of P1
fn mirror(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    return vec3f(1.0, 0.0, 0.0);
}



@vertex
fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut 
{
    const pos = array<vec2f, 4 > (vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
    var vsOut : VSOut;
    vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
    vsOut.coords = pos[VertexIndex];
    return vsOut; 
}




@fragment
fn main_fs(@location(0) coords: vec2f) -> @location(0) vec4f
{
    let ipcoords = vec2f(coords.x*uniforms.aspect*0.5, coords.y*0.5);
    var ray = get_camera_ray(ipcoords);
    var hit = HitInfo(false,0,vec3f(0.0,0.0,0.0),vec3f(0.0,0.0,0.0),vec3f(0.0,0.0,0.0),0);
    let plane_hit = intersect_scene(&ray,&hit);
    if (plane_hit)
    {
        return vec4f(shade(&ray, &hit), 1.0);
    }
    return vec4f(0.1, 0.3, 0.6, 1.0);
}
    </script>
    Please use a browser that supports HTML5 canvas.
    </canvas>
    </body>

    </html>
