<!DOCTYPE html>
<html>

<head>
<title > W5P5</title>
<script type="text/javascript" src="../common/MV.js"></script>
<script type="text/javascript" src="../common/OBJParser.js"></script>
<script type = "text/javascript" src = "w5p5.js"></script>
</head>

<body>
<div>
    <label for="options">Plane/triangle shader:</label>
        <select id="planeTriangleOptions" name="planeTriangleOptions">
            <option value="Lambertian" selected>Lambertian</option>
            <option value="Phong">Phong</option>
            <option value="Mirror">Mirror</option>
            <option value="Base">Base</option>
        </select>
    <div>
        <select id="addressmode" size="2">
                <option value="0">clamp-to-edge</option>
                <option value="1" selected>repeat</option>
            </select>
        <select id="filtermode" size="2">
                <option value="0">nearest</option>
                <option value="1" selected>linear</option>
            </select>

        <label for="useTexture">Use Texture:</label>
        <input type="checkbox" id="useTexture" name="useTexture" value="1" checked>
        

        <label for="pixelSubdivision">Pixel Subdivision:</label>
        <input type="range" id="pixelSubdivision" name="pixelSubdivision" min="1" max="10" value="1" oninput="this.nextElementSibling.value = this.value">
        <output>1</output>
    </div>
    
    <br>
<canvas id = "webgpu-canvas" width = "512" height = "512">
<script id = "wgsl" type = "x-shader">

const PI = 3.14;

const PLANE_POSITION: vec3f = vec3f(0.0, 0.0, 0.0);
const PLANE_COLOR : vec3f = vec3f(0.1, 0.7, 0.0);
const SPHERE_CENTER: vec3f = vec3f(0.0, 0.5, 0.0);
const SPHERE_RADIUS: f32 = 0.3;
const SPHERE_COLOR: vec3f = vec3f(0.0, 0.0, 0.0);
const TRIANGLE_COLOR: vec3f = vec3f(0.9, 0.9, 0.9);
const TRIANGLE_VERTICES: array<vec3f, 3> = array<vec3f, 3>(
    vec3f(-0.2, 0.1, 0.9),  // First vector
    vec3f(0.2, 0.1, 0.9),  // Second vector
    vec3f(-0.2, 0.1, -0.1)   // Third vector
);

const LIGHT_POSITION: vec3f = vec3f(0.0, 1.0, 0.0);
const DIRECTIONAL_LIGHT_DIRECTION: vec3f = vec3f(-1.0);
//const LIGHT_INTENSITY: vec3f = vec3f(PI, PI, PI);
const LIGHT_INTENSITY: vec3f = vec3f(50, 50, 50);

const DIFFUSE_REFLECTANCE: vec3f = vec3f(0.9);
const AMBIENT_LIGHT: vec3f = vec3f(0.1);

const T_MIN = 10e-2;
const T_MAX = 1.0e32;

const IOR_GLASS = 1.5;
const IOR_AIR = 1.0;

const SPECULAR_SPHERE: f32 = 0.1;
const SHININESS_SPHERE: f32 = 6.0;

const TEXTURE_SCALE: f32 = 0.2;


struct Uniforms {
    eyePos: vec3<f32>,
    const_cam: f32, // Placed next to eyePos to avoid padding
    lookAt: vec3<f32>,
    aspect: f32, // Placed next to lookAt to avoid padding
    up: vec3<f32>,
    padding: f32,
};

struct Uniforms_uint {
    sphere_shader: u32,
    plane_triangle_shader: u32,
    use_repeat: u32,
    use_linear: u32,
    use_texture: u32,
    pixel_subdivision: u32,
};

struct Material {
    color: vec3<f32>,
    emission: vec3<f32>,
    specular: vec3<f32>,
    illum: f32,
    padding3: vec3<f32>,
};



@group(0) @binding(0) var<uniform> uniforms : Uniforms;
@group(0) @binding(1) var<uniform> uniforms_uint : Uniforms_uint;
@group(0) @binding(2) var my_texture: texture_2d<f32>;
@group(0) @binding(3) var<storage> jitter: array<vec2f>;
@group(0) @binding(4)
var<storage> vPositions : array<vec3f>;
@group(0) @binding(5)
var<storage> meshFaces : array<vec3u>;
@group(0) @binding(6) var<storage> vNormals : array<vec3f>;
@group(0) @binding(7) var<storage> materials : array<Material>;
@group(0) @binding(8) var<storage> material_indices : array<u32>;
@group(0) @binding(9) var<storage> lightIndices : array<u32>;
struct VSOut {
    @builtin(position) position : vec4f,
    @location(0) coords : vec2f,
};

struct Ray {
    origin: vec3f,
    direction: vec3f,
    tmin: f32,
    tmax: f32,
};

struct HitInfo {
    has_hit: bool,
    dist: f32,
    position: vec3f,
    normal: vec3f,
    color: vec3f,
    shader: u32,
    ior1_over_ior2: f32,
    specular: f32,
    shininess: f32,
    ambient: vec3f,
    diffuse: vec3f,
    texcoords: vec2f,
};

struct Light {
    L_i: vec3f,
    w_i: vec3f,
    dist: f32
};

fn get_camera_ray(ipcoords: vec2f) -> Ray
{
    let v = normalize(uniforms.lookAt - uniforms.eyePos);
    let u = uniforms.up;
    var b1 = normalize(cross(v, u));
    let b2 = cross(b1, v);
    return Ray(uniforms.eyePos, normalize(b1 * ipcoords.x + b2 * ipcoords.y + v * uniforms.const_cam), T_MIN, T_MAX);
}


fn intersect_sphere(ray: ptr<function, Ray>, hit: ptr<function, HitInfo>, center: vec3f, radius: f32) -> bool
{
    let b_2 = dot(ray.origin - center, ray.direction);
    let c = dot((ray.origin - center), (ray.origin - center)) - (radius*radius);
    if ( (b_2*b_2 - c) < 0.0)
    {
        return false; // no intersection
    }
    let t_1 = -b_2 - sqrt((b_2*b_2)-c);
    let t_2 = -b_2 + sqrt((b_2*b_2)-c);

    var t = ray.tmax; // Some large number to represent infinity
    if t_1 > 0.0 {
        if t_1 >= ray.tmin && t_1 <= ray.tmax {
            t = t_1; // Use t_1 if it's valid
        }
    }
    if t_2 > 0.0 && t_2 < t {
            if t_2 >= ray.tmin && t_2 <= ray.tmax {
            t = t_2; // Use t_1 if it's valid
        }   
    }

    if t <= ray.tmin || t >= ray.tmax {
        return false; // t is not within ray bounds
    }
    //update hit info
    (*ray).tmax = t;
    (*hit).has_hit = true;
    (*hit).dist = t;
    (*hit).position = ray.origin + t * ray.direction;
    (*hit).normal = normalize((*hit).position - center);
    (*hit).color = SPHERE_COLOR;
    (*hit).shader = u32(uniforms_uint.sphere_shader);
    (*hit).specular = SPECULAR_SPHERE;
    (*hit).shininess = SHININESS_SPHERE;
    return true;
    
}

fn intersect_triangle(ray: ptr<function, Ray>, hit: ptr<function, HitInfo>, faceIndex: u32, use_texture: bool, use_repeat: bool, use_linear: bool) -> bool
    
{
    let face = meshFaces[faceIndex];
    let e0 = vPositions[face[1]] - vPositions[face[0]];
    let e1 = vPositions[face[2]] - vPositions[face[0]];

    var normal = cross(e0, e1);
    let beta = dot(cross((vPositions[face[0]] - ray.origin), ray.direction), e1) / (dot(ray.direction, normal));

    let gamma = - dot( cross(vPositions[face[0]] - ray.origin, ray.direction), e0) / dot(ray.direction,normal);

    if (beta < 0.0) || (gamma < 0.0) || (beta+gamma>1.0)
    {
        return false;
    }
    let weighted_normal = normalize( vNormals[face[0]] * (1.0 - beta - gamma) + vNormals[face[1]] * beta + vNormals[face[2]] * gamma);   
    let t = dot((vPositions[face[0]] - ray.origin), normal) / dot(ray.direction,normal);
    if  (t <= ray.tmin || t >= ray.tmax)
    {
        return false;
    }

    if (use_texture)
    {
        //Not implemented
        let uv = vec2f(0.0);

        (*hit).color = texture(my_texture, (*hit).texcoords, use_repeat, use_linear);
    }

    //update hit info
    (*ray).tmax = t;
    (*hit).has_hit = true;
    (*hit).dist = t;
    (*hit).position = ray.origin + t * ray.direction;
    (*hit).normal = weighted_normal;
    (*hit).color = materials[material_indices[faceIndex]].color; 
    (*hit).shader = uniforms_uint.plane_triangle_shader;
    (*hit).ambient = materials[material_indices[faceIndex]].emission;
    (*hit).diffuse = DIFFUSE_REFLECTANCE;
    (*hit).specular = SPECULAR_SPHERE;
    (*hit).shininess = SHININESS_SPHERE;
    return true;

}

fn intersect_scene(ray: ptr<function, Ray>, hit: ptr<function, HitInfo>, use_repeat: bool, use_linear: bool, use_texture: bool) -> bool 
{

// Define scene data as constants.
// Call an intersection function for each object.
// For each intersection found, update (*r).tmax and store additional info about the hit.
    //intersect_plane(ray, hit, PLANE_POSITION, plane_onb, use_repeat, use_linear, use_texture);
    //intersect_sphere(ray, hit, SPHERE_CENTER, SPHERE_RADIUS);

    for (var faceIndex: u32 = 0; faceIndex < arrayLength(&meshFaces); faceIndex = faceIndex + 1) {
        var temp_hit = *hit;
        if (intersect_triangle(ray, &temp_hit, faceIndex, use_texture, use_repeat, use_linear)) {
            if (!(*hit).has_hit || temp_hit.dist < (*hit).dist) {
                *hit = temp_hit;
            }
        }
    }

    return (*hit).has_hit;
}

fn sample_point_light(pos: vec3f) -> Light
{
    let L_i = LIGHT_INTENSITY / ( length(LIGHT_POSITION - pos) * length(LIGHT_POSITION - pos));
    let w_i = normalize(LIGHT_POSITION - pos);
    let dist = length(LIGHT_POSITION - pos);
    return Light(L_i, w_i, dist);
}

fn sample_directional_light() -> Light
{
    return Light(LIGHT_INTENSITY, normalize(-DIRECTIONAL_LIGHT_DIRECTION), T_MAX);
}

fn sample_area_light(triangeIndex: u32, hit: ptr<function, HitInfo>) -> Light
{
    //calculate the centtroid of the triangle to position the point light source
    let face = meshFaces[triangeIndex];
    let centroid = (vPositions[face[0]] + vPositions[face[1]] + vPositions[face[2]]) / 3.0;
    let w_i = normalize(centroid - (*hit).position);
    //calculate the interpolated normal of the light triangle

    let edge1 = vPositions[face[1]] - vPositions[face[0]];
    let edge2 = vPositions[face[2]] - vPositions[face[0]];
    let area = 0.5 * length(cross(edge1, edge2));
    let lightNormal = normalize(cross(edge1, edge2));
    let cosThetaE = max(-dot(w_i, lightNormal), 0.0);

    let dist = length(centroid - (*hit).position);
    let L_e = materials[material_indices[triangeIndex]].emission;
    let L_i = area * cosThetaE * L_e / (dist * dist);
    return Light(L_i, w_i, dist);
}
fn shade(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    switch (*hit).shader {
        case 1 { return lambertian(r, hit); }
        case 2 { return phong(r, hit); }
        case 3 { return mirror(r, hit); }
        case 4 { return refraction(r, hit); }
        case 5 { return glossy(r, hit); }
        case default { return (*hit).color + (*hit).ambient; }
    }
}

fn lambertian(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    //let light = sample_point_light((*hit).position);
    //let light = sample_directional_light();

    var L_o = (*hit).ambient;
    var L_rd = vec3f(0.0);
    var lightContributions = 0;
    for (var i: u32 = 0; i < arrayLength(&lightIndices); i = i + 1) 
        {
        lightContributions += 1;
        let light = sample_area_light(lightIndices[i], hit);
        var shadow_ray = Ray((*hit).position, light.w_i, T_MIN, light.dist-T_MIN);
        var shadow_hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0),0,0,0,0,vec3f(0.0),vec3f(0.0),vec2f(0.0));
        if (intersect_scene(&shadow_ray, &shadow_hit, false, false, false))
        {
            if ((shadow_hit.dist) < (*hit).dist)
            {
                continue;
            }
        }
        let n = normalize((*hit).normal);
        L_rd += (*hit).diffuse / PI * light.L_i * max(dot(n, light.w_i), 0.0) * (*hit).color * f32(lightContributions);
        
        }
    if (lightContributions == 0)
    {
        return L_o;
    }
    return L_rd / f32(lightContributions)+ L_o;
}

//not used as of P1
fn phong(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    //let light = sample_point_light((*hit).position);
    let light = sample_directional_light();
    var shadow_ray = Ray((*hit).position, light.w_i, T_MIN, light.dist);
    var shadow_hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0),0,0,0,0,vec3f(0.0),vec3f(0.0),vec2f(0.0));
    if (intersect_scene(&shadow_ray, &shadow_hit, false, false, false))
    {
        if ((shadow_hit.dist) < (*hit).dist)
        {
            return vec3f(0.0);
        }
    }
    let k_d = (*hit).diffuse / PI * (*hit).color;
    let k_s = (*hit).specular * ((*hit).shininess + 2.0) / (2.0 * PI);
    let n = normalize((*hit).normal);
    let w_r = reflect(-light.w_i, n);
    let w_o = -r.direction;
    let L_r = light.L_i * (k_d+ k_s * pow(max(dot(w_r, w_o), 0.0), (*hit).shininess)) * dot(n, light.w_i);
    return L_r;
   // return test_L;
}

fn mirror(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    //let light = sample_point_light((*hit).position);
    let direction = reflect(r.direction, (*hit).normal);
    *r = Ray((*hit).position, direction, T_MIN, T_MAX);
    (*hit).has_hit = false;
    (*hit).color = vec3f(0.0, 0.0, 0.0);
    return (*hit).color;

}

fn refraction(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    // Find if the ray hits from inside or outside
    let n1 = IOR_AIR; 
    let n2 = IOR_GLASS;
    var cos_theta_i = dot(-r.direction, (*hit).normal);
    
    //if the ray from inside of the object, swap the normal and the ior
    if cos_theta_i < 0.0
    {
        cos_theta_i = -cos_theta_i;
        (*hit).normal = -(*hit).normal;
        (*hit).ior1_over_ior2 = n2/n1;
    }
    else
    {
        (*hit).ior1_over_ior2 = n1/n2;
    }

    let cos_theta_t2 = 1.0 - ((*hit).ior1_over_ior2 * (*hit).ior1_over_ior2 * (1.0 - cos_theta_i * cos_theta_i));
    
    // Check for total internal reflection
    if (cos_theta_t2 < 0.0)
    {
        return mirror(r, hit);
    }

    let refracted_direction = 
        (*hit).ior1_over_ior2 * (cos_theta_i * (*hit).normal - (-r.direction)) - 
        (*hit).normal * sqrt(cos_theta_t2);

    // all of the above is equivalent to the following built-in webgpu function ....
    //let refracted_direction_t = refract(r.direction, (*hit).normal, (*hit).ior1_over_ior2);

    *r = Ray((*hit).position, normalize(refracted_direction), T_MIN, T_MAX);
    
    (*hit).has_hit = false;
    return (*hit).color;
}

fn glossy(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
{
    // call phong and refraction functions and mix the results
    let phong_result = phong(r, hit);
    let refraction_result = refraction(r, hit);
    return phong_result + refraction_result;
}

fn texture_nearest(texture: texture_2d<f32>, texcoords: vec2f, repeat: bool) -> vec3f {
    let res = textureDimensions(texture);
    let st = select(clamp(texcoords, vec2f(0), vec2f(1)), texcoords - floor(texcoords), repeat);
    let ab = st*vec2f(res);
    let UV = vec2u(ab + 0.5) % res;
    let texcolor = textureLoad(texture, UV, 0);
    return texcolor.rgb;
}

fn texture_linear(texture: texture_2d<f32>, texcoords: vec2f, repeat: bool) -> vec3f {
    let res = textureDimensions(texture);
    let st = select(clamp(texcoords, vec2f(0), vec2f(1)), texcoords - floor(texcoords), repeat);
    let ab = st*vec2f(res);
    // find the four nearest texels
    let UV = vec2u(ab);
    let texcolor = textureLoad(texture, UV, 0);
    let texcolor1 = textureLoad(texture, vec2u(UV.x+1, UV.y), 0);
    let texcolor2 = textureLoad(texture, vec2u(UV.x, UV.y+1), 0);
    let texcolor3 = textureLoad(texture, vec2u(UV.x+1, UV.y+1), 0);
    // interpolate between the four texels
    let frac = ab - vec2f(UV);
    let texcolor4 = mix(texcolor, texcolor1, frac.x);
    let texcolor5 = mix(texcolor2, texcolor3, frac.x);
    let texcolor6 = mix(texcolor4, texcolor5, frac.y);
    return texcolor6.rgb;
}

fn texture(texture: texture_2d<f32>, texcoords: vec2f, repeat: bool, linear: bool) -> vec3f {
    if (linear)
    {
        return texture_linear(texture, texcoords, repeat);
    }
    else
    {
        return texture_nearest(texture, texcoords, repeat);
    }
}



@vertex
fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut 
{
    const pos = array<vec2f, 4 > (vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
    var vsOut : VSOut;
    vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
    vsOut.coords = pos[VertexIndex];
    return vsOut; 
}




@fragment
fn main_fs(@location(0) coords: vec2f) -> @location(0) vec4f
{
    let use_repeat = uniforms_uint.use_repeat != 0;
    let use_linear = uniforms_uint.use_linear != 0;
    let use_texture = uniforms_uint.use_texture != 0;
    const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
    const max_depth = 10;
    let base_uv = vec2f(coords.x*uniforms.aspect*0.5f, coords.y*0.5f);
    // Add jitter to the pixel calculation
    var pixel_subdivision = uniforms_uint.pixel_subdivision;
    var result = vec3f(0.0);
    for (var i: u32 = 0; i < pixel_subdivision; i++)
    {
        for (var j: u32 = 0; j < pixel_subdivision; j++)
        {
            let idx = (i*pixel_subdivision + j) * 2;
            let uv = base_uv + jitter[idx];
            var r = get_camera_ray(uv);
            var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0),0,0,0,0,vec3f(0.0),vec3f(0.0),vec2f(0.0));
            for(var i = 0; i < max_depth; i++) {
                if(intersect_scene(&r, &hit, use_repeat, use_linear, use_texture))
                { 
                    result += shade(&r, &hit); 
                }
                else 
                { 
                    result += bgcolor.rgb;
                    break; 
                }
                if(hit.has_hit) 
                { 
                    break; 
                }
            }
        }
    }
    
    result = result / (f32(pixel_subdivision) * f32(pixel_subdivision));
    return vec4f(result, 1.0);
}
    </script>
    Please use a browser that supports HTML5 canvas.
    </canvas>
    </div>
    </body>

    </html>
